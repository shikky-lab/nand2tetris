// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    Mux16(a=x,b=false,sel=zx,out=xout1);
    Not16(in=xout1,out=xoutnot);
    Mux16(a=xout1,b=xoutnot,sel=nx,out=xout2);

    Mux16(a=y,b=false,sel=zy,out=yout1);
    Not16(in=yout1,out=youtnot);
    Mux16(a=yout1,b=youtnot,sel=ny,out=yout2);

    Add16(a=xout2,b=yout2,out=outAdd);
    And16(a=xout2,b=yout2,out=outAnd);

    Mux16(a=outAnd,b=outAdd,sel=f,out=pout);

    Not16(in=pout,out=nout);

    Mux16(a=pout,b=nout,sel=no,out=out);
    Mux16(a=pout,b=nout,sel=no,out[0..7]=outStoredL,out[8..15]=outStoredH);
    Mux16(a=pout,b=nout,sel=no,out[0..14]=outStoredL14,out[15]=outStored15);

    Or8Way(in=outStoredL,out=orL);
    Or8Way(in=outStoredH,out=orH);
    Or(a=orL,b=orH,out=orOut);
    //Or16Way(in=outStored,out=orOut);
    Not(in=orOut,out=zr);

    And(a=true,b=outStored15,out=ng);

}